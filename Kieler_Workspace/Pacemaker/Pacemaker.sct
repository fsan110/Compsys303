
scchart VVIPacemaker{
   
  //@note External input signals present in multiple regions need a 
  //	  connector/blank state for transitions
  //	  Blank/connector states are also needed for priority to work 
  //	  in the event they transition to a common state. Region VRP and LRI have examples
  //	  This problem does not happen for internal signals. Region LRI_timer 
  //	  is an example
  //@detail Potential bug. LRI timer can be stopped during VRP. This implies that 
  //	    we cannot have an LRI shorter than VRP.
  
  
  //External Environment
  input signal VSense, VRPTO, LRITO;
  output signal VPace;
 
 initial state top{
   
    signal VRP_ex, VRP_start;
    signal LRI_start, LRI_ex, LRI_stop;
 
    //1
   // @hide
    
    region VRP_timer :
    initial state idle
    --> timer with VRP_start;

    state timer
    --> idle  with VRPTO/ VRP_ex;
    
    
    //2
    // @hide
 	region VRP:
    initial state idle   
  
      	--> waitVRP1 with VSense
     	--> waitVRP1 with VPace ;
   
  	connector state waitVRP1
  	
  		--> waitVRP immediate with /VRP_start;


	state waitVRP
	
		--> idle with VRP_ex
		--> connectState1 with VSense;
	
	connector state connectState1
	
		--> waitVRP immediate;
	

    //3
    region LRI:
        
    initial state init
  		--> connectInit with VSense;
    
    connector state connectInit
   	    --> waitVRP immediate with /LRI_start;
   
   	state waitVRP
   		--> waitLRI with VRP_ex;
   
    state waitLRI
   		--> connectVSense with VSense
   		--> connectLRI_ex with LRI_ex ;

   	connector state connectVSense
   		--> intState immediate with /LRI_stop;
   	
   	
  	connector state connectLRI_ex
   		--> intState immediate with /VPace;
   	
	//Reset state 
	
    state intState
    	-->waitVRP with /LRI_start;
    
    //4
    region LRI_timer :
    initial state idle
    --> timer with LRI_start;
    
    state timer
    --> idle with LRI_stop
    --> idle with LRITO/LRI_ex;
  
	};

}
