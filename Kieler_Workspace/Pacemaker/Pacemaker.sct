
scchart VVIPacemaker{
   
  //@note External input signals present in multiple regions need a 
  //	  connector/blank state for transitions
  //	  Blank/connector states are also needed for priority to work 
  //	  in the event they transition to a common state.
  // 	  By sets and relations theory each state can have
  //	  one unique output for each transition
  
  
  
  //Control, external inputs and status signals
  input signal VSense, VRPTO, LRITO;
  output signal VPace;
  /*Timer signals*/
  //VRP
  output signal VRP_start, VRP_stop;
  //LRI
  output signal LRI_start, LRI_stop;
  
  
  
 initial state ControlUnit{
   
    //Variables
    output bool VRP_ex, LRI_ex = false;
	
   //1

   region VRP_timer:
   		initial state idle
   			--> timer with VRP_start / VRP_ex = false;
   	
   		state timer
   			--> idle with VRPTO / VRP_ex = true;
	
	
	region VRP:
	
		initial state idle
			--> connectVRP with VPace
			--> connectVRP with VSense;
			
		connector state connectVRP
			--> waitVRP immediate with / VRP_start;	
    	
    	state waitVRP
    		--> idle with VRP_ex / VRP_stop
    		--> waitVRP with VSense;
    		
    	
   	
    region LRI_timer:
    	initial state idle
    		--> timer with LRI_start / LRI_ex = false;
    	
    	state timer
    		--> idle with LRI_stop / LRI_ex = true
    		--> idle with LRITO / LRI_ex = true;
    	
    region LRI:
    	initial state idle
    		--> waitVRP with VSense / LRI_start;
    		
    	state waitVRP
    		--> waitLRI with VRP_ex;
    	
    	//Timer will be stopped with either LRI_stop or LRI_ex	
    	state waitLRI
    		--> intState with VSense / LRI_stop
    		--> connectLRI with LRI_ex;
    		    	
    	connector state connectLRI
    		--> intState immediate with / VPace;
    					
    	state intState
    		--> waitVRP with / LRI_start;	
   };
}
