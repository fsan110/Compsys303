
scchart VVIPacemaker{
   
  //@note External input signals present in multiple regions need a connector/blank state for transitions
  //	  Blank/connector states are also needed for priority to work in the event they transition to a common state
  //	  region VRP and LRI have examples
  //	  This problem does not happen for internal signals. region LRI_timer is an example
  //@detail Potential bug. LRI timer can be stopped during VRP. This implies that we cannot have an LRI shorter than VRP
  
  //External Environment
  input signal VSense, VRPTO, LRITO;
  output signal VPace;
 
 initial state top{
   
    signal VRP_ex, VRP_start;
    signal LRI_start, LRI_ex, LRI_stop;
 
    //1
    region VRP_timer :
    initial state idle
    --> timer with VRP_start;

    state timer
    --> idle  with VRPTO/ VRP_ex;
    
    
    //2
 	region VRP:
    initial state idle   
    --> waitVRP1 with VSense
    --> waitVRP1 with VPace ;
   
  	state waitVRP1
  	--> waitVRP2 immediate with /VRP_start;


	state waitVRP2
	--> idle with VRP_ex
	--> connectState1 with VSense;
	
	connector state connectState1
	--> waitVRP2 immediate;
	

    //3
    region LRI:
        
    initial state init
  	--> connectInit with VSense;
    
    connector state connectInit
    --> waitVRP immediate with /LRI_start;
   
   	state waitVRP
   	--> waitLRI with VRP_ex;
   
    state waitLRI
   	--> connectVSense with VSense
   	--> intState with LRI_ex / VPace;
   	//--> connectLRI_ex with LRI_ex;
   	
   	state connectVSense
   	--> intState immediate with /LRI_stop;
   	
//   	state connectLRI_ex
//   	--> intState immediate with /VPace;
   	
	//Reset state 
    state intState
    -->waitVRP with /LRI_start;
    
    //4
    region LRI_timer :
    initial state idle
    --> timer with LRI_start;
    
    state timer
    --> idle with LRI_stop
    --> idle with LRITO/LRI_ex;
  
	};

}
